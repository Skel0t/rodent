/* Performs a pooling on the given matrix with receptive field "size x size" and
   stride "size". The pooling_fn is a function that operates on the values in the
   receptive field. The values are given in the order left to right, top to bottom.

   Does NOT allow pooling in-place, as it is using parallelism and by pooling
   in-place, other threads might read wrong values that were written in-place
   from another pooling thread. To pool in-place, please use pool_inplace. */
fn @pool_cpu(img_mat: Matrix, size: i32, pooling_fn: fn(fn(i32) -> f32) -> f32, buf: Buffer, off: i64) -> Matrix {
    let res_mat = make_matrix(buf, off, img_mat.format, img_mat.channels, img_mat.rows / size, img_mat.cols / size);
    let img_acc = get_mat_acc(img_mat);

    cpu_profile(&mut pool_counter, || {
        for r_acc, _v, row, col, chn in iterate_matrix_par(res_mat) {
            /* Needs at least size-many elements.
              Although known at compile-time AnyDSL does only support magic numbers here. */
            let mut v : [f32 * 4];
            fn @values(idx: i32) -> f32 {
                v(idx)
            }

            for r in unroll(0, size) {
                for c in unroll(0, size) {
                    v(r * size + c) = img_acc.read(size * row + r, size * col + c, chn);
                }
            }

            let val = pooling_fn(values);
            r_acc.write(row, col, chn, val);
        }
    });

    res_mat
}

/* Does the same as pool_cpu, but without parallelism and therefore allows in-place pooling. */
fn @pool_cpu_inplace(img_mat: Matrix, size: i32, pooling_fn: fn(fn(i32) -> f32) -> f32, buf: Buffer, off: i64) -> Matrix {
    let res_mat = make_matrix(buf, off, img_mat.format, img_mat.channels, img_mat.rows / size, img_mat.cols / size);
    let img_acc = get_mat_acc(img_mat);

    /* Needs at least size-many elements.
       Although known at compile-time AnyDSL does only support magic numbers here. */
    let mut v : [f32 * 4];
    fn @values(idx: i32) -> f32 {
        v(idx)
    }
    cpu_profile(&mut pool_counter, || {
        for r_acc, _v, row, col, chn in iterate_matrix(res_mat) {
            for r in unroll(0, size) {
                for c in unroll(0, size) {
                    v(r * size + c) = img_acc.read(size * row + r, size * col + c, chn);
                }
            }

            let val = pooling_fn(values);
            r_acc.write(row, col, chn, val);
        }
    });

    res_mat
}

/* Like pool_cpu, just on the GPU. */
fn @pool_gpu(img_mat: Matrix, size: i32, pooling_fn: fn(fn(i32) -> f32) -> f32, buf: Buffer, off: i64) -> Matrix {
    let acc = cuda_accelerator(0);

    let res_mat = make_matrix(buf, off, img_mat.format, img_mat.channels, img_mat.rows / size, img_mat.cols / size);
    let img_acc = get_mat_acc(img_mat);
    let r_acc   = get_mat_acc(res_mat);

    let threads_x = 32;
    let threads_y = 32;
    let block = (threads_x, threads_y, 1);
    let grid  = (round_up(res_mat.cols, threads_x), round_up(res_mat.rows, threads_y), res_mat.channels);

    cpu_profile(&mut pool_counter, || {
        for work_item in acc.exec(grid, block) {
            let col = work_item.bdimx() * work_item.bidx() + work_item.tidx();
            let row = work_item.bdimy() * work_item.bidy() + work_item.tidy();
            let chn = work_item.bidz();     // Will never overshoot, always valid

            /* Needs at least size-many elements.
            Although known at compile-time AnyDSL does only support magic numbers here. */
            let mut v : [f32 * 4];
            fn @values(idx: i32) -> f32 {
                v(idx)
            }

            if row < res_mat.rows && col < res_mat.cols {
                // Valid thread, do same calculation as in cpu here
                for r in unroll(0, size) {
                    for c in unroll(0, size) {
                        v(r * size + c) = img_acc.read(size * row + r, size * col + c, chn);
                    }
                }
                let val = pooling_fn(values);
                r_acc.write(row, col, chn, val);
            }
        }
    });

    res_mat
}

/* Explicitly performs nearest-neighbor upsampling, saving the result at buf at position off. */
fn @nearest(img_mat: Matrix, buf: Buffer, off: i64) -> Matrix {
    let res_mat = make_matrix(buf, off, img_mat.format, img_mat.channels, img_mat.rows * 2, img_mat.cols * 2);

    let img_acc = get_mat_acc(img_mat);

    cpu_profile(&mut nearest_counter, || {
        for res_acc, _v, row, col, chn in iterate_matrix_par(res_mat) {
            res_acc.write(row, col, chn, img_acc.read(row / 2, col / 2, chn));
        }
    });


    res_mat
}

/* Returns an accessor which acts like accessing an image that was upsampled using nearest-neighbor. */
fn @nearest_acc(img_mat: Matrix) -> AccM {
    let acc = get_mat_acc(img_mat);

    AccM {
        read  = @|row, col, chn|      { acc.read(row / 2, col / 2, chn) },
        write = @|row, col, chn, val| { acc.write(row / 2, col / 2, chn, val) }
    }
}
